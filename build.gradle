plugins {
  id 'org.asciidoctor.jvm.convert' version '3.3.2' apply false
}

defaultTasks "clean", "build"

configure(allprojects) {

  repositories {
    mavenCentral()
  }

  apply plugin: "java"
  apply plugin: "idea"

  group     = "com.seitenbau.stu"

  sourceCompatibility   = 1.8
  targetCompatibility   = 1.8

  ext.commonsdbcpVersion            = "1.4"
  ext.commonsIoVersion              = "2.9.0"
  ext.commonsLangVersion            = "2.6"
  ext.dbunitVersion                 = "2.7.0"
  ext.easymockVersion               = "4.3"
  ext.festAssertVersion             = "1.4"
  ext.guavaVersion                  = "30.1.1-jre"
  ext.groovyAllVersion              = "3.0.8"
  ext.hamcrestAllVersion            = "1.3"
  ext.hibernatePersistanceVersion   = "1.0.2.Final"
  ext.hibernateEntityManagerVersion = "5.5.0.Final"
  ext.hsqldbVersion                 = "2.6.0"
  ext.jodaTimeVersion               = "2.10.10"
  ext.junitVersion                  = "4.13.2"
  ext.logbackVersion                = "1.2.3"
  ext.miglayoutSwingVersion         = "11.0"
  ext.mockitoAllVersion             = "1.10.19"
  ext.mysqlConnVersion              = "8.0.25"
  ext.rsyntaxtextareaVersion        = "3.1.2"
  ext.slf4jVersion                  = "1.7.30"
  ext.solrjVersion                  = "4.10.4" // "8.8.2" from https://solr.apache.org/docs/5_5_0/solr-solrj/deprecated-list.html : org.apache.solr.client.solrj.impl.HttpSolrServer -> org.apache.solr.client.solrj.impl.HttpSolrClient
  ext.spockVersion                  = "2.0-groovy-3.0"
  ext.springDataVersion             = "2.5.1"
  ext.springVersion                 = "5.3.7"
  ext.thoughtworksXstreamVersion    = "1.4.17"
  ext.velocityVersion               = "1.7"

  ext.sourceEncoding                = "ISO-8859-1"
  ext.gradleScriptDir               = "${rootProject.projectDir}/gradle"


  compileJava {
    options.encoding = project.sourceEncoding
  }

  apply from: "$gradleScriptDir/publish-maven.gradle"
  apply from: "$gradleScriptDir/eclipse.gradle"
  
  test {
        reports.html.enabled = false
  }
}

configure(subprojects) {
  apply plugin: "groovy"

  dependencies {
    testCompile("org.codehaus.groovy:groovy-all:$groovyAllVersion")
    testCompile("org.spockframework:spock-core:$spockVersion")
  }

  test {
     testLogging {
        exceptionFormat "full" // default is "short"
     }
  }
}

project("stu-common") {
  dependencies {
    compile("org.apache.velocity:velocity:$velocityVersion")
    compile("commons-io:commons-io:$commonsIoVersion")
    compile("commons-lang:commons-lang:$commonsLangVersion")
    compile("org.easytesting:fest-assert:$festAssertVersion")
    compile("org.hamcrest:hamcrest-all:$hamcrestAllVersion", optional)
    compile("org.easymock:easymock:$easymockVersion", optional)
    compile("junit:junit:$junitVersion", optional)
    compile("org.mockito:mockito-all:$mockitoAllVersion", optional)
    compile("org.springframework:spring-beans:$springVersion", optional)
    compile("ch.qos.logback:logback-classic:$logbackVersion", optional)
    compile("com.google.guava:guava:$guavaVersion", optional)
    compile("joda-time:joda-time:$jodaTimeVersion", optional)
  }
}

project("stu-database") {
  dependencies {
    compile project(':stu-common')

    compile("junit:junit:$junitVersion", optional)
    compile("commons-dbcp:commons-dbcp:$commonsdbcpVersion", optional)
    compile("org.dbunit:dbunit:$dbunitVersion"){
      exclude module: 'slf4j-api'
      exclude module: 'slf4j-nop'
    }
    compile("org.mockito:mockito-all:$mockitoAllVersion", optional)
    compile("org.easytesting:fest-assert:$festAssertVersion")
    compile("org.apache.solr:solr-solrj:$solrjVersion", optional)
    compile("org.apache.velocity:velocity:$velocityVersion")
    compile("commons-lang:commons-lang:$commonsLangVersion")
    compile("org.slf4j:slf4j-api:$slf4jVersion")
    compile("com.thoughtworks.xstream:xstream:$thoughtworksXstreamVersion")
    compile("com.miglayout:miglayout-swing:$miglayoutSwingVersion")
    compile("com.fifesoft:rsyntaxtextarea:$rsyntaxtextareaVersion")
    compile("org.codehaus.groovy:groovy-all:$groovyAllVersion")
    compile("com.google.guava:guava:$guavaVersion")
    
    runtime("mysql:mysql-connector-java:$mysqlConnVersion")

    testCompile("org.slf4j:slf4j-log4j12:$slf4jVersion")
    testCompile( "org.apache.ant:ant-junit:1.8.4" ) {
        exclude(group: 'junit', module: 'junit')
    }
  }
}

project("stu-examples") {

  ext {
    outputDatasetDslDir = "$projectDir/src/dataset-dsl/java"
    srcDatasetModelDir  = "$projectDir/src/dataset-model/java"
  }

  dependencies {
    compile project(':stu-database')
    runtime("org.hsqldb:hsqldb:$hsqldbVersion")
    compile("org.springframework:spring-context:$springVersion")
    compile("org.springframework:spring-orm:$springVersion")
    compile("org.springframework:spring-test:$springVersion")
    compile("org.springframework.data:spring-data-jpa:$springDataVersion")
    compile("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:$hibernatePersistanceVersion")
    compile("org.hibernate:hibernate-entitymanager:$hibernateEntityManagerVersion")
    compile("org.codehaus.groovy:groovy-all:$groovyAllVersion")

    testCompile("org.spockframework:spock-spring:$spockVersion")
  }
  
  sourceSets {
    dataSetModel {
      java {
        srcDir srcDatasetModelDir
      }
      compileClasspath += configurations.compile
      runtimeClasspath += configurations.runtime
    }
    dataSetDsl {
      java {
        srcDir outputDatasetDslDir
      }
      compileClasspath = configurations.compile
    }
    test {
      compileClasspath += sourceSets.dataSetDsl.output
      runtimeClasspath += sourceSets.dataSetDsl.output
    }
  }

  task generateDataSetDsl(dependsOn: 'dataSetModelClasses', type: JavaExec) {
    ext.destDir   = file(outputDatasetDslDir)
    inputs.dir      srcDatasetModelDir
    outputs.dir     destDir
    main          = 'model.Generator'
    args          = [destDir]
    classpath     = sourceSets.dataSetModel.runtimeClasspath
    doFirst {
       destDir.mkdirs()
    }
  }

  clean {
    delete outputDatasetDslDir
  }

  idea {
     module {
       sourceDirs += file(srcDatasetModelDir)
       sourceDirs += file(outputDatasetDslDir)
     }
  }
  ideaModule.dependsOn("generateDataSetDsl")

  eclipseClasspath.dependsOn("generateDataSetDsl")
  compileDataSetDslJava.dependsOn("generateDataSetDsl")
  compileTestGroovy.dependsOn("compileDataSetDslJava")
}

project("stu-documentation") {

  apply plugin: 'org.asciidoctor.jvm.convert'

  ext {
    htmlDir = "$projectDir/html"
  }
  
  asciidoctor {
    sourceDir = new File("$projectDir/src")
    outputDir = new File("$htmlDir")
    attributes = [
      'source-highlighter': 'coderay',
      toc: '',
      idprefix: '',
      idseparator: '-',
      copycss: '',
    ]
  }
  
  clean {
    delete htmlDir
  }

}
